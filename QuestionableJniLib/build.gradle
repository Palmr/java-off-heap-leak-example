plugins {
    id 'idea'
    id 'java'
    id 'c'
}

group 'uk.co.palmr'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

def JNI_INCLUDE_DIR = System.properties['java.home'] + "/../include"

model {
    components {
        QuestionableJniLib(NativeLibrarySpec) {
            sources {
                c {
                    source {
                        srcDir 'src/main/c'
                    }
                }
            }
        }
    }
    toolChains {
        gcc(Gcc) {
            eachPlatform {
                if(System.properties['os.name'].equals("Mac OS X")) {
                    cCompiler.withArguments {  args ->
                        args << "-I" + JNI_INCLUDE_DIR
                        args << "-I" + JNI_INCLUDE_DIR + "/darwin"
                        args << "-std=gnu11"
                        args << "-g"
                    }
                } else {
                    cCompiler.withArguments {  args ->
                        args << "-I" + JNI_INCLUDE_DIR
                        args << "-I" + JNI_INCLUDE_DIR + "/linux"
                        args << "-std=gnu11"
                        args << "-g"
                    }
                }
            }
        }
    }
}
